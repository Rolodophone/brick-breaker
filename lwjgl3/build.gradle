plugins {
	id 'org.beryx.runtime' version '1.12.4'
}

apply plugin: 'application'

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'io.github.rolodophone.brickbreaker.lwjgl3.Lwjgl3LauncherKt'
def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
eclipse.project.name = appName + '-lwjgl3'
sourceCompatibility = 8.0

dependencies {
	implementation project(':core')
	implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

import org.gradle.internal.os.OperatingSystem

run {
	workingDir = rootProject.file('assets').path
	setIgnoreExitValue(true)
	
	if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}

jar {
	archiveBaseName = appName
	dependsOn configurations.runtimeClasspath
	from {
		configurations.runtimeClasspath.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
	doLast {
		file(archivePath).setExecutable(true, false)
	}
	destinationDirectory = file("$buildDir/lib")
}

jpackageImage.dependsOn jar

runtime {
	options.addAll(['--strip-debug',
			   '--compress', '2',
			   '--no-header-files',
			   '--no-man-pages',
			   '--strip-native-commands',
			   '--vm', 'server'])
	modules.addAll(['java.base' ,
			   'java.desktop',
			   'jdk.unsupported'])
	distDir.set(file(buildDir))

	javaHome.set("/usr/lib/jvm/java-15-openjdk")

	jpackage {
		//jpackageHome = '/usr/lib/jvm/open-jdk'
		mainJar = jar.archiveFileName.get()
//		if (osName.contains('windows')) {
//			imageOptions = ["--icon", file("../icons/icon.ico")]
//		} else if (osName.contains('linux')) {
//			imageOptions = ["--icon", file("../icons/icon.png")]
//		} else if (osName.contains('mac')) {
//			imageOptions = ["--icon", file("../icons/icon.icns")]
//		}
		//TODO add icons and uncomment above
	}
}